000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. AOC-2016-D4P2.
000300
000400 ENVIRONMENT DIVISION.
000500 INPUT-OUTPUT SECTION.
000600 FILE-CONTROL.
000700     SELECT INPUTFILE ASSIGN TO 'INPUT'
000800     ORGANIZATION IS LINE SEQUENTIAL.
000900
001000 DATA DIVISION.
001100 FILE SECTION. 
001200 FD INPUTFILE IS EXTERNAL 
001300     RECORD IS VARYING IN SIZE
001400     DATA RECORD IS INPUT-LINE.
001500 01 INPUT-LINE   PIC X(999).
001600
001700 WORKING-STORAGE SECTION. 
001800 01 LOOP         PIC 9      VALUE 1.
001900 01 LOOP2        PIC 99     VALUE 1.
002000 01 LEN          PIC 999.
002100 01 LEN2         PIC 999.
002200 01 TEMP         PIC X(999).
002300 01 ROOM-NAME    PIC A(99).
002400 01 SECTOR-ID    PIC 999.
002500 01 CHECKSUM     PIC AAAAA.
002600 01 FREQUENCIES.
002700    05 FREQ-MAP OCCURS 26 TIMES.
002800       10 LTRS   PIC A.
002900       10 FREQ   PIC 99.
003000 01 LETTERS      PIC A(26)  VALUE 'abcdefghijklmnopqrstuvwxyz'. 
003100
003200 PROCEDURE DIVISION.
003300 MAIN.
003400     OPEN INPUT INPUTFILE.
003500     PERFORM UNTIL LOOP = 0
003600             READ INPUTFILE NEXT RECORD INTO INPUT-LINE
003700             AT END
003800                MOVE 0 TO LOOP
003900             NOT AT END
004000                 PERFORM PARSE-LINE
004100             END-READ
004200     END-PERFORM
004300     CLOSE INPUTFILE.
004400     DISPLAY SECTOR-ID.
004500     GOBACK.
004600
004700 PARSE-LINE.
004800     MOVE 0 TO LEN
004900
005000*    FORMAT LINE
005100     MOVE FUNCTION REVERSE(INPUT-LINE) TO TEMP
005200     INSPECT TEMP REPLACING FIRST '-' BY ','
005300     MOVE FUNCTION REVERSE(TEMP) TO INPUT-LINE
005400
005500     INSPECT INPUT-LINE REPLACING ALL '[' BY ','
005600     INSPECT INPUT-LINE REPLACING ALL ']' BY SPACE
005700
005800*    SPLIT INTO PRIMARY SECTIONS
005900     UNSTRING INPUT-LINE DELIMITED BY ','
006000        INTO TEMP SECTOR-ID CHECKSUM
006100     END-UNSTRING
006200     
006300*    ISOLATE ROOM ID
006400     MOVE 0 TO LEN
006500     INSPECT TEMP TALLYING LEN FOR CHARACTERS BEFORE SPACE
006600
006700     MOVE ' ' TO ROOM-NAME
006800     PERFORM VARYING LEN2 FROM 1 BY 1 UNTIL LEN2 > LEN
006900             IF TEMP(LEN2:1) IS NOT EQUAL TO '-' THEN
007000                STRING ROOM-NAME TEMP(LEN2:1) DELIMITED BY SPACE
007100                   INTO ROOM-NAME
007200                END-STRING
007300             END-IF
007400     END-PERFORM
007500
007600*    MAP LETTERS TO FREQUENCY AND SORT
007700     MOVE 0 TO LEN
007800     INSPECT ROOM-NAME TALLYING LEN FOR CHARACTERS BEFORE SPACE
007900     PERFORM VARYING LOOP2 FROM 1 BY 1 UNTIL LOOP2 > 26
008000             MOVE 0 TO LEN2
008100             INSPECT ROOM-NAME(1:LEN) TALLYING LEN2
008200                FOR ALL LETTERS(LOOP2:1)
008300             MOVE LEN2 TO FREQ(LOOP2)
008400             MOVE LETTERS(LOOP2:1) TO LTRS(LOOP2)
008500     END-PERFORM
008600
008700     SORT FREQ-MAP DESCENDING FREQ
008800
008900     IF LTRS(1) = CHECKSUM(1:1) AND
009000        LTRS(2) = CHECKSUM(2:1) AND
009100        LTRS(3) = CHECKSUM(3:1) AND
009200        LTRS(4) = CHECKSUM(4:1) AND
009300        LTRS(5) = CHECKSUM(5:1) THEN
009400        PERFORM DECRYPT
009500     END-IF.
009600
009700 DECRYPT.
009800     PERFORM VARYING LOOP2 FROM 1 BY 1 UNTIL LOOP2 > LEN
009900             MOVE LETTERS(FUNCTION MOD(
010000                FUNCTION ORD(ROOM-NAME(LOOP2:1)) +
010100                FUNCTION MOD(SECTOR-ID, 26) - 97, 26):1)
010200                TO ROOM-NAME(LOOP2:1)
010300     END-PERFORM 
010400
010500     MOVE 0 TO LEN2
010600     INSPECT ROOM-NAME(1:LEN) TALLYING LEN2 FOR CHARACTERS BEFORE
010700        "northpoleobjectstorage"
010800     
010900     IF LEN2 = ZERO THEN
011000        MOVE 0 TO LOOP
011100     END-IF.
011200
011300 END PROGRAM AOC-2016-D4P2.
